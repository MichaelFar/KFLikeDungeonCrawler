# ProtoController v1.0 by Brackeys
# CC0 License
# Intended for rapid prototyping of first-person games.
# Happy prototyping!

extends CharacterBody3D

class_name Player
## Can we move around?
@export var can_move : bool = true
## Are we affected by gravity?
@export var has_gravity : bool = true
## Can we press to jump?
@export var can_jump : bool = true
## Can we hold to run?
@export var can_sprint : bool = false
## Can we press to enter freefly mode (noclip)?
@export var can_freefly : bool = false

@export_group("Speeds")
## Look around rotation speed.
@export var look_speed : float = 0.002
## Normal speed.
@export var base_speed : float = 7.0
## Speed of jump.
@export var jump_velocity : float = 4.5
## How fast do we run?
@export var sprint_speed : float = 10.0
## How fast do we freefly?
@export var freefly_speed : float = 25.0

@export var crouch_speed : float = 3.5

@export var creep_speed_coefficient : float = .3

@export_group("Input Actions")
## Name of Input Action to move Left.
@export var input_left : String = "Left"
## Name of Input Action to move Right.
@export var input_right : String = "Right"
## Name of Input Action to move Forward.
@export var input_forward : String = "Forward"
## Name of Input Action to move Backward.
@export var input_back : String = "Back"
## Name of Input Action to Jump.
@export var input_jump : String = "ui_accept"
## Name of Input Action to Sprint.
@export var input_sprint : String = "sprint"
## Name of Input Action to toggle freefly mode.
@export var input_freefly : String = "freefly"

@export var stealthManager : StealthManager

var mouse_captured : bool = false
var look_rotation : Vector2
var move_speed : float = 0.0
var freeflying : bool = false

## IMPORTANT REFERENCES
@export var head: Node3D 
@export var collider: CollisionShape3D
@export var camera : Camera3D
@export var standingCollider : CollisionShape3D
var initialHeadPosition : Vector3
@export var initialCrouchCameraDestination : Marker3D
@export var crouchCheckRaycast : RayCast3D
signal Moving
signal Stopped

var isMoving : bool = false

var isCrouching : bool = false :
	set(value):
		isCrouching = value
		crouchCheckRaycast.enabled = isCrouching
	get():
		return isCrouching

var isCreeping : bool = false

var original_speed : float

#@export var texture
var updateLightRate : float = .1
var deltaCounter : float = 0
func _ready() -> void:
	check_input_mappings()
	initialHeadPosition = head.position
	look_rotation.y = rotation.y
	look_rotation.x = head.rotation.x
	Global.camera = camera
	Global.player = self
	original_speed = base_speed
	
	#crouchCheckRaycast.enabled = isCrouching
	
func _unhandled_input(event: InputEvent) -> void:
	# Mouse capturing
	if Input.is_mouse_button_pressed(MOUSE_BUTTON_LEFT):
		capture_mouse()
	if Input.is_key_pressed(KEY_ESCAPE):
		release_mouse()
	
	# Look around
	if mouse_captured and event is InputEventMouseMotion:
		rotate_look(event.relative)
	
	# Toggle freefly mode
	if can_freefly and Input.is_action_just_pressed(input_freefly):
		if not freeflying:
			enable_freefly()
		else:
			disable_freefly()

func _physics_process(delta: float) -> void:
	#print("Frame rate is " + str(1 / delta))
	# If freeflying, handle freefly and nothing else
	deltaCounter += delta
	if can_freefly and freeflying:
		var input_dir := Input.get_vector(input_left, input_right, input_forward, input_back)
		var motion := (head.global_basis * Vector3(input_dir.x, 0, input_dir.y)).normalized()
		motion *= freefly_speed * delta
		move_and_collide(motion)
		return
	
	# Apply gravity to velocity
	if has_gravity:
		if not is_on_floor():
			velocity += get_gravity() * delta

	# Apply jumping
	if can_jump:
		if Input.is_action_just_pressed(input_jump) and is_on_floor():
			velocity.y = jump_velocity

	# Modify speed based on sprinting
	if can_sprint and Input.is_action_pressed(input_sprint):
			move_speed = sprint_speed
	else:
		move_speed = base_speed

	# Apply desired movement to velocity
	if can_move:
		var input_dir := Input.get_vector(input_left, input_right, input_forward, input_back)
		var move_dir := (transform.basis * Vector3(input_dir.x, 0, input_dir.y)).normalized()
		if move_dir:
			
			velocity.x = move_dir.x * move_speed
			velocity.z = move_dir.z * move_speed
			
			if(!isMoving):
				isMoving = true
				Moving.emit()
			
		else:
			
			if(isMoving):
				
				isMoving = false
				Stopped.emit()
				
			velocity.x = move_toward(velocity.x, 0, move_speed)
			velocity.z = move_toward(velocity.z, 0, move_speed)
	else:
		
		velocity.x = 0
		velocity.y = 0
		
	
	# Use velocity to actually move
	move_and_slide()


func _input(event: InputEvent) -> void:
	
	base_speed = original_speed
	
	var final_speed_coefficient = 1.0
	if (event.is_action_released("Crouch")):
		crouch()
	
	isCreeping = event.is_action_pressed("Creep", true)
	
	if(isCrouching):
		final_speed_coefficient /= 2
	if(isCreeping):
		final_speed_coefficient /= 2
	
	base_speed = original_speed * final_speed_coefficient
	
## Rotate us to look around.
## Base of controller rotates around y (left/right). Head rotates around x (up/down).
## Modifies look_rotation based on rot_input, then resets basis and rotates by look_rotation.
func crouch():
	
	if(!crouchCheckRaycast.is_colliding()):
		var destination = initialCrouchCameraDestination.position.y if !isCrouching else initialHeadPosition.y
		
		isCrouching = !isCrouching
		
		standingCollider.disabled = isCrouching
		#base_speed = crouch_speed if isCrouching else original_speed
		var tween = get_tree().create_tween()
		tween.set_trans(tween.TRANS_SINE)
		tween.set_ease(Tween.EASE_IN)
		tween.tween_property(head, "position:y",  destination, 0.2)
	
func rotate_look(rot_input : Vector2):
	look_rotation.x -= rot_input.y * look_speed
	look_rotation.x = clamp(look_rotation.x, deg_to_rad(-85), deg_to_rad(85))
	look_rotation.y -= rot_input.x * look_speed
	transform.basis = Basis()
	rotate_y(look_rotation.y)
	head.transform.basis = Basis()
	head.rotate_x(look_rotation.x)


func enable_freefly():
	collider.disabled = true
	freeflying = true
	velocity = Vector3.ZERO

func disable_freefly():
	collider.disabled = false
	freeflying = false


func capture_mouse():
	Input.set_mouse_mode(Input.MOUSE_MODE_CAPTURED)
	mouse_captured = true


func release_mouse():
	Input.set_mouse_mode(Input.MOUSE_MODE_VISIBLE)
	mouse_captured = false


## Checks if some Input Actions haven't been created.
## Disables functionality accordingly.
func check_input_mappings():
	if can_move and not InputMap.has_action(input_left):
		push_error("Movement disabled. No InputAction found for input_left: " + input_left)
		can_move = false
	if can_move and not InputMap.has_action(input_right):
		push_error("Movement disabled. No InputAction found for input_right: " + input_right)
		can_move = false
	if can_move and not InputMap.has_action(input_forward):
		push_error("Movement disabled. No InputAction found for input_forward: " + input_forward)
		can_move = false
	if can_move and not InputMap.has_action(input_back):
		push_error("Movement disabled. No InputAction found for input_back: " + input_back)
		can_move = false
	if can_jump and not InputMap.has_action(input_jump):
		push_error("Jumping disabled. No InputAction found for input_jump: " + input_jump)
		can_jump = false
	if can_sprint and not InputMap.has_action(input_sprint):
		push_error("Sprinting disabled. No InputAction found for input_sprint: " + input_sprint)
		can_sprint = false
	if can_freefly and not InputMap.has_action(input_freefly):
		push_error("Freefly disabled. No InputAction found for input_freefly: " + input_freefly)
		can_freefly = false

func check_stealth(checked_ai : BaseEnemy, distance_limit : float = 5.0) -> float:
	return stealthManager.calculate_stealth_level(checked_ai, distance_limit)
